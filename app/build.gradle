/*
 * Copyright (c) 2017 Nicholas van Dyke. All rights reserved.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.vandyke.sia"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 3000
        versionName "0.3.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        /* lets us not shrink debug builds, for faster build times. Not necessary for release builds, since we shrink those */
        multiDexEnabled true
    }

    buildTypes {
        release {
            lintOptions {
                checkReleaseBuilds false
            }
            shrinkResources true
            /* Using this combination of the following two options lets us minify (remove unused methods + classes)
             * using the Android Gradle Plugin's minifier, which doesn't obfuscate and can also be used in incremental builds */
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    splits {
        /* see app/src/main/cpp/main.c for why we use ABI splits */
        abi {
            enable true
            reset()
            include 'arm64-v8a', 'x86'
            universalApk false
        }
    }

    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
    }

    androidExtensions {
        experimental = true
    }

    /* needed for material-intro library */
    dataBinding {
        enabled = true
    }
}

dependencies {
    ext.support_version = "27.1.0"
    
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    /* kotlin stuff */
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.21'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.core:core-ktx:0.3'

    /* testing */
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'org.amshove.kluent:kluent-android:1.35'
    testImplementation 'org.amshove.kluent:kluent:1.35'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    /* support library */
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    /* UI */
    implementation("com.mikepenz:materialdrawer:6.0.6@aar") { transitive = true }
    implementation 'co.zsmb:materialdrawer-kt:1.3.3'
    implementation 'com.github.medyo:android-about-page:1.2.2'
    implementation 'com.github.kenglxn.QRGen:android:2.3.0'
    implementation 'me.dm7.barcodescanner:zxing:1.9.3'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'io.github.tonnyl:light:1.0.0'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'it.sephiroth.android.library.targettooltip:target-tooltip-library:1.3.15'

    /* simplified preferences */
    implementation 'com.chibatching.kotpref:kotpref:2.2.0'
    implementation 'com.chibatching.kotpref:enum-support:2.2.0'
    implementation 'com.takisoft.fix:preference-v7-simplemenu:27.1.0.0'
    implementation 'com.takisoft.fix:preference-v7:27.1.0.0'

    /* networking */
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

    /* RxJava */
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    /* Android Architecture Components */
    implementation 'android.arch.lifecycle:runtime:1.1.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    kapt 'android.arch.lifecycle:compiler:1.1.1'

    /* Room, for local data persistence */
    implementation "android.arch.persistence.room:runtime:1.1.0-beta2"
    implementation "android.arch.persistence.room:rxjava2:1.1.0-beta2"
    kapt "android.arch.persistence.room:compiler:1.1.0-beta2"
    implementation "android.arch.paging:runtime:1.0.0-beta1"

    /* Dagger, for DI */
    implementation 'com.google.dagger:dagger:2.12'
    kapt 'com.google.dagger:dagger-compiler:2.12'

    /* Moshi (JSON converter) */
    implementation 'com.squareup.moshi:moshi:1.5.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'

    /* for crash reporting/analytics */
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.1'
    implementation 'com.google.firebase:firebase-core:15.0.0'

    implementation 'com.android.billingclient:billing:1.0'

    /* check for leaks */
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

repositories {
    mavenCentral()
}

/* ensure the no-op leakcanary dependency is always used in unit tests */
configurations.all { config ->
    if (config.name.contains('UnitTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
